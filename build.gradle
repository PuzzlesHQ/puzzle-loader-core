import dev.puzzleshq.accesswriter.AccessWriters
import me.lucko.jarrelocator.JarRelocator
import me.lucko.jarrelocator.Relocation
import org.gradle.api.artifacts.transform.TransformParameters;
import dev.puzzleshq.buildsrc.GenericTransformer

plugins {
    id 'java'
    id 'maven-publish'
    id 'com.github.johnrengelman.shadow'
}

group = 'dev.puzzleshq'
version = '1.0.0'

repositories {
    mavenCentral()
    maven { url "https://libraries.minecraft.net" }
    maven {
        url = "https://jitpack.io"
    }
    maven {
        name = "meteor-maven"
        url = "https://maven.meteordev.org/releases"
    }
    mavenLocal()
}

sourceSets {
    common
    client
}

configurations {

    allCompileOnly
    clientCompilyOnly.extendsFrom(allCompileOnly)
    commonCompilyOnly.extendsFrom(allCompileOnly)

    clientBundle
    clientImplementation.extendsFrom(clientBundle)

    clientInternal
    clientImplementation.extendsFrom(clientInternal)

    commonBundle
    commonImplementation.extendsFrom(commonBundle)
    clientImplementation.extendsFrom(commonBundle)
}

def natives(String lwjgl_natives) {
    dependencies {
        clientImplementation "org.lwjgl:lwjgl::$lwjgl_natives"
        clientImplementation "org.lwjgl:lwjgl-opengl::$lwjgl_natives"
        clientImplementation "org.lwjgl:lwjgl-glfw::$lwjgl_natives"
    }
}

abstract class MyTransform implements TransformAction<TransformParameters.None> {

    @InputArtifact
    abstract Provider<FileSystemLocation> getInputArtifact();

    @Override
    void transform(TransformOutputs outputs) {
        var inp = inputArtifact.get().asFile;
        var out = outputs.file(inp.name.replace(".jar", "-transformed.jar"));

        List<Relocation> rules = new ArrayList<>();
        rules.add(new Relocation("org.objectweb", "bundled.org.objectweb"));
        rules.add(new Relocation("org.spongepowered.include", "bundled"));
        rules.add(new Relocation("com.google.common.eventbus", "unused"));

        JarRelocator relocator = new JarRelocator(inp, out, rules);
        relocator.run();

        AccessWriters.init(null)
        AccessWriters.MERGED.add("public", "org/spongepowered/asm/mixin/transformer/MixinTransformer")
        AccessWriters.MERGED.add("public", "org/spongepowered/asm/mixin/transformer/MixinTransformer", "<init>", "()V")
        AccessWriters.MERGED.add("public", "org/spongepowered/asm/mixin/MixinEnvironment", "gotoPhase", '(Lorg/spongepowered/asm/mixin/MixinEnvironment$Phase;)V')

        GenericTransformer.transform(out)
    }
}
Attribute<Boolean> manipulated = Attribute.of("manipulated", Boolean.class);

afterEvaluate {
    project.getDependencies().getAttributesSchema().attribute(manipulated);
    project.getDependencies().getArtifactTypes().getByName("jar", artifact -> {
        artifact.getAttributes().attribute(manipulated, false);
    });

    project.getDependencies().registerTransform(MyTransform) {
        getFrom().attribute(manipulated, false);
        getTo().attribute(manipulated, true);
    }

    project.getConfigurations().all(config -> {
        if (config.isCanBeResolved())
            config.getAttributes().attribute(manipulated, true);
    });
}

// Common Dependencies
dependencies {
    clientImplementation 'org.jetbrains:annotations:24.0.0'
    clientImplementation platform("org.lwjgl:lwjgl-bom:$lwjgl_version")

    clientImplementation "org.lwjgl:lwjgl"
    clientImplementation "org.lwjgl:lwjgl-opengl"
    clientImplementation "org.lwjgl:lwjgl-glfw"
    natives("natives-linux")
    natives("natives-linux-arm32")
    natives("natives-linux-arm64")
    natives("natives-macos")
    natives("natives-macos-arm64")
    natives("natives-windows")
    natives("natives-windows-arm64")
    natives("natives-windows-x86")
    clientBundle "org.joml:joml:${joml_version}"

    clientInternal sourceSets.common.output

    // Common

    commonImplementation 'org.jetbrains:annotations:24.0.0'

    commonBundle annotationProcessor("io.github.llamalad7:mixinextras-fabric:$mixin_extras_version")
    commonBundle("net.fabricmc:sponge-mixin:$mixin_version") {
        exclude group: "com.google.code.gson", module: "gson"
        exclude group: "com.google.guava", module: "guava"
    }
    commonBundle "net.sf.jopt-simple:jopt-simple:$jopt_simple_version"

    commonBundle "com.github.zafarkhaja:java-semver:0.10.2"

    commonBundle 'org.slf4j:slf4j-ext:1.7.23'
    commonBundle 'org.slf4j:slf4j-api:1.7.23'
    commonBundle "org.apache.logging.log4j:log4j-api:$slf4j_version"
    commonBundle "org.apache.logging.log4j:log4j-core:$slf4j_version"
    commonBundle "org.apache.logging.log4j:log4j-slf4j-impl:$slf4j_version"

    commonBundle "org.reflections:reflections:$reflections_version"
    commonBundle("net.neoforged:bus:$eventbus_version") {
        exclude group: "org.apache.logging.log4j", module: "log4j-api"
    }

    commonBundle "org.ow2.asm:asm:$asm_version"
    commonBundle "org.ow2.asm:asm-tree:$asm_version"
    commonBundle "org.ow2.asm:asm-util:$asm_version"
    commonBundle "org.ow2.asm:asm-analysis:$asm_version"
    commonBundle "org.ow2.asm:asm-commons:$asm_version"

    commonBundle "dev.puzzleshq:access-writer:1.0.1"
    commonBundle "dev.puzzleshq:mod-format-framework:1.0.0"

    commonBundle "org.hjson:hjson:$hjson_version"

}

tasks.register("buildMergeJar", com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar) {
    ArrayList t = new ArrayList();
    Collections.addAll(t, project.configurations.clientInternal, project.configurations.commonBundle, project.configurations.clientBundle)
    configurations = t

    mergeServiceFiles()
    relocate("org.objectweb", "internal.org.objectweb")
    relocate("org.spongepowered.include", "internal")
    relocate("com.google.common.eventbus", "unused")

    archiveClassifier = "merged"
    from sourceSets.client.output
}

tasks.register("buildCommonJar", com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar) {
    configurations = Collections.singletonList(project.configurations.commonBundle)

    mergeServiceFiles()
    relocate("org.objectweb", "internal.org.objectweb")
    relocate("org.spongepowered.include", "internal")
    relocate("com.google.common.eventbus", "unused")

    archiveClassifier = "common"
    from sourceSets.common.output
}

tasks.register("buildClientJar", com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar) {
    configurations = Collections.singletonList(project.configurations.clientBundle)

    mergeServiceFiles()
    relocate("org.objectweb", "internal.org.objectweb")
    relocate("org.spongepowered.include", "internal")
    relocate("com.google.common.eventbus", "unused")

    archiveClassifier = "client"
    from sourceSets.client.output
}

tasks.register("buildStrippedMergeJar", com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar) {
    configurations = Collections.singletonList(project.configurations.clientInternal)

    mergeServiceFiles()
    relocate("org.objectweb", "internal.org.objectweb")
    relocate("org.spongepowered.include", "internal")
    relocate("com.google.common.eventbus", "unused")

    archiveClassifier = "merged-stripped"
    from sourceSets.client.output
}

tasks.register("buildStrippedCommonJar", com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar) {
    mergeServiceFiles()
    relocate("org.objectweb", "internal.org.objectweb")
    relocate("org.spongepowered.include", "internal")
    relocate("com.google.common.eventbus", "unused")

    archiveClassifier = "common-stripped"
    from sourceSets.common.output
}

tasks.register("buildStrippedClientJar", com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar) {
    mergeServiceFiles()
    relocate("org.objectweb", "internal.org.objectweb")
    relocate("org.spongepowered.include", "internal")
    relocate("com.google.common.eventbus", "unused")

    archiveClassifier = "client-stripped"
    from sourceSets.client.output
}

processCommonResources {
    def replacement_properties = [
            "version": ((String)version).contains(".") ? version : "69.69.69"
    ]

    inputs.properties replacement_properties
    replacement_properties.put("project", project)

    filesMatching(["assets/puzzle-loader/core-version.txt"]) {
        expand replacement_properties
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = group
            artifactId = "puzzle-loader-core"

            artifact source: buildMergeJar, classifier: 'merged', extension: 'jar'
            artifact source: buildClientJar, classifier: 'client', extension: 'jar'
            artifact source: buildCommonJar, classifier: 'common', extension: 'jar'
            artifact source: buildStrippedMergeJar, classifier: 'merged-stripped', extension: 'jar'
            artifact source: buildStrippedClientJar, classifier: 'client-stripped', extension: 'jar'
            artifact source: buildStrippedCommonJar, classifier: 'common-stripped', extension: 'jar'
        }
    }
}

wrapper {
    gradleVersion = "8.5"
    distributionType = Wrapper.DistributionType.ALL
}

java.toolchain.languageVersion.set(JavaLanguageVersion.of(24))
